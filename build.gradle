import org.apache.commons.io.FileUtils
import org.apache.tools.ant.filters.ReplaceTokens

import java.time.Instant

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.6.20'
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

mainClassName = 'at.xirado.bean.Bean'

group 'at.xirado'
version '5.0.0'

shadowJar {
    archiveFileName = "Bean.jar"
}

compileJava.options.encoding = 'UTF-8'
repositories {
    mavenCentral()
    maven { url 'https://m2.dv8tion.net/releases' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://m2.chew.pro/releases' }
    jcenter()
}

/**
 * Clears resources folder for processResources task
 */
tasks.create("clearResources") {
    FileUtils.deleteDirectory(new File(project.rootDir, 'build/resources/main'))
}

tasks.build {
    dependsOn(tasks.clearResources)
    dependsOn(tasks.shadowJar)
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation 'com.github.Xirado:JDA:2ae1b1a'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.0'
    implementation 'com.sparkjava:spark-core:2.9.3'
    implementation 'org.codehaus.groovy:groovy-jsr223:3.0.8'
    implementation 'org.jline:jline:3.20.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.mariadb.jdbc:mariadb-java-client:2.7.3'
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation 'com.zaxxer:HikariCP:5.0.0'
    implementation 'org.fusesource.jansi:jansi:2.4.0'
    implementation 'com.google.guava:guava:31.0.1-jre'
    implementation 'pw.chew:jda-chewtils:1.20.2'
    implementation 'org.scilab.forge:jlatexmath:1.0.7'
    implementation 'com.github.Xirado:Lavalink-Client:041082f'
    implementation "club.minnced:discord-webhooks:0.7.5"
    implementation 'io.github.classgraph:classgraph:4.8.146'
    implementation 'com.github.minndevelopment:jda-ktx:9f01b74'

    // Prometheus
    implementation "io.prometheus:simpleclient:0.14.1"
    implementation "io.prometheus:simpleclient_hotspot:0.14.1"
    implementation "io.prometheus:simpleclient_httpserver:0.14.1"
    implementation 'com.facebook:ktfmt:0.36'

    runtimeOnly 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.6.21'

}

processResources {
    filesMatching("**/app.properties") {
        filter ReplaceTokens, tokens: [
                "app-version": version,
                "build-time" : Instant.now().toEpochMilli() + ''
        ]
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "16"
    }
}
